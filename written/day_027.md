# 100 DAYS OF CODE : Day 27
**Note: Code samples are likely to be used from documentation**

## Optionals
Optionals allow Zig to set values to either a value or `null`, this is the only time Zig will let you use the `null` value type. An optional value is prefixed with a `?`. You can get the value from optionals by either using `optional.?` or just using the variable name, for pointers you must use `variable.?.*` to fetch the value.

```zig
const expect = @import("std").testing.expect;

test "optional type" {
    // Declare an optional and coerce from null:
    var foo: ?i32 = null;

    // Coerce from child type of an optional
    foo = 1234;

    // Use compile-time reflection to access the child type of the optional:
    comptime try expect(@typeInfo(@TypeOf(foo)).Optional.child == i32);
}
```

With optionals, we are able to introduce the `orelse` keyword. This allows us to assign a default value in case the optional provided is null. It will automatically unwrap our optional data and assess whether it is `null` or not.

```zig
// malloc prototype included for reference
extern fn malloc(size: size_t) ?*u8;

fn doAThing() ?*Foo {
    // Here malloc returns null and so we set our zig ptr to null
    const ptr = malloc(1234) orelse return null;
    // ...
}
```

*Or simply put:*
```zig
// value will be assigned to the rhs (10), since the lhs is null
var value: ?u8 = null orelse 10;
```

## Error Sets / Unions
An error set is essentially just a collection of enum values and each error name gets assigned an unsigned integer value greater than 0 accross the entire compilation. You can coerce an error from a subset to a superset: 

```zig
const std = @import("std");

const FileOpenError = error {
    AccessDenied,
    OutOfMemory,
    FileNotFound,
};

const AllocationError = error {
    OutOfMemory,
};

test "coerce subset to superset" {
    const err = foo(AllocationError.OutOfMemory);
    try std.testing.expect(err == FileOpenError.OutOfMemory);
}

fn foo(err: AllocationError) FileOpenError {
    return err;
}
```

An error set may differ between architectures and different error sets are required, so how would we handle this? Do we make a new error set with the platform dependant error codes? An easier way is to combine the existing sets into a new set, removing the need of manually building a new set and code.

*Combining existing error sets into a new set:*
```zig
const A = error{
    NotDir,

    /// A doc comment
    PathNotFound,
};
const B = error{
    OutOfMemory,

    /// B doc comment
    PathNotFound,
};

const C = A || B;

fn foo() C!void {
    return error.NotDir;
}

test "merge error sets" {
    if (foo()) {
        @panic("unexpected");
    } else |err| switch (err) {
        error.OutOfMemory => @panic("unexpected"),
        error.PathNotFound => @panic("unexpected"),
        error.NotDir => {},
    }
}
```

Error union types can be combined with a normal type using a `!` to form an error union. It is likely to use an error union type more often than the error set type by itself. Using a union type allows a function to either return an error or the value specified, including void.

There are some ways of handling the returned type with either a `try` or a `catch`. These keywords are not like other programming languages, where they are used in a block.

*We can use a catch to assign a default value on error:*
```zig
fn doAThing(str: []u8) void {
    const number = parseU64(str, 10) catch 13;
    // ...
}
```

*The try keyword allows for the error to be passed up to the caller:*
```zig
fn doAThing(str: []u8) !void {
    const number = try parseU64(str, 10);
    // ...
}
```

When we know *for sure* that a function will not fail, we can use the `unreachable` value, this tells the compiler to panic in `Debug` and `ReleaseSafe` modes but causes undefined behaviour in `ReleaseFast` mode. So if an error does occur where we think it shouldn't, we can patch it up.

*An example of a function that should always succeed:*
```zig
const number = parseU64("1234", 10) catch unreachable;
```

We know this is hardcoded and will eventually change to accept an input of some degree, so we can modify the code to look like this:

```zig
fn doAThing(str: []u8) void {
    if (parseU64(str, 10)) |number| {
        doSomethingWithNumber(number);
    } else |err| switch (err) {
        error.Overflow => {
            // handle overflow...
        },
        // we promise that InvalidChar won't happen (or crash in debug mode if it does)
        error.InvalidChar => unreachable,
    }
}
```

Sometimes an error may occur during a certain process and we would like to return elegantly, especially if we are handling files or other data. This is where `errdefer` comes in. Just like `defer` it occurs at the end of the function, acting like the exit, so that we're able to call certain code when we get an error inside the function.

```zig
fn createFoo(param: i32) !Foo {
    const foo = try tryToAllocateFoo();
    // now we have allocated foo. we need to free it if the function fails.
    // but we want to return it if the function succeeds.
    errdefer deallocateFoo(foo);

    const tmp_buf = allocateTmpBuffer() orelse return error.OutOfMemory;
    // tmp_buf is truly a temporary resource, and we for sure want to clean it up
    // before this block leaves scope
    defer deallocateTmpBuffer(tmp_buf);

    if (param > 1337) return error.InvalidParam;

    // here the errdefer will not run since we're returning success from the function.
    // but the defer will run!
    return foo;
}
```

## Pointer Types
- `*T` : Single-item pointer to one item
    - Supports deref syntax `ptr.*`
- `[*]T` : Many-item pointer to unknown number of items
    - Supports index syntax `ptr[i]`
    - Supports slice syntax `ptr[start..end]`
    - Supports pointer arithmetic `ptr + x` `ptr + y`
    - `T` must have a known type
- `*[N]T` : Pointer to N items, same as single-item pointer to an array
    - Supports index syntax `array_ptr[i]`
    - Supports slice syntax `array_ptr[start..end]`
    - Supports len property `array_ptr.len`
- `[]T` : Pointer to run-time known number of items
    - Supports index syntax `slice[i]`
    - Supports slice syntax `slice[start..end]`
    - Supports len property `slice.len`

Use a `&x` to obtain a single-item pointer. We are also able to turn pointers into `optional` which will allow them to be `null`.

*Optional pointers:*
```zig
const expect = @import("std").testing.expect;

test "optional pointers"
{
    var ptr: ?*i32 = null;

    var x: i32 = 100;
    ptr = &x;

    try expect(ptr.?.* == 100);
}
```

*Using pointers and testing types:*
```zig
const expect = @import("std").testing.expect;

test "address of syntax" {
    // Get the address of a variable:
    const x: i32 = 1234;
    const x_ptr = &x;

    // Dereference a pointer:
    try expect(x_ptr.* == 1234);

    // When you get the address of a const variable, you get a const single-item pointer.
    try expect(@TypeOf(x_ptr) == *const i32);

    // If you want to mutate the value, you'd need an address of a mutable variable:
    var y: i32 = 5678;
    const y_ptr = &y;
    try expect(@TypeOf(y_ptr) == *i32);
    y_ptr.* += 1;
    try expect(y_ptr.* == 5679);
}

test "pointer array access" {
    // Taking an address of an individual element gives a
    // single-item pointer. This kind of pointer
    // does not support pointer arithmetic.
    var array = [_]u8{ 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 };
    const ptr = &array[2];
    try expect(@TypeOf(ptr) == *u8);

    try expect(array[2] == 3);
    ptr.* += 1;
    try expect(array[2] == 4);
}
```

In Zig, `Slices` are prefered over `Sentinal-Terminated Pointers`. It is possible to turn an array or pointer into a slice using slice syntax. Pointers are capable of working at `comptime`, as long as the code does not depend on undefined memory.

*Slicing:*
```zig
const expect = @import("std").testing.expect;

test "pointer slicing" {
    var array = [_]u8{ 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 };
    const slice = array[2..4];
    try expect(slice.len == 2);

    try expect(array[3] == 4);
    slice[1] += 1;
    try expect(array[3] == 5);
}
```

*Comptime pointers:*
```zig
const expect = @import("std").testing.expect;

test "comptime pointers" {
    comptime {
        var x: i32 = 1;
        const ptr = &x;
        ptr.* += 1;
        x += 1;
        try expect(ptr.* == 3);
    }
}
```

### @intToPtr @ptrToInt and comptime
There is no explicit conversion between pointers and integers in Zig, you must use a builtin function to handle it. These can also be handled in a comptime block and the pointer addresses will be preserved as long as they are never dereferenced.

```zig
const expect = @import("std").testing.expect;

test "@ptrToInt and @intToPtr" {
    const ptr = @intToPtr(*i32, 0xdeadbee0);
    const addr = @ptrToInt(ptr);
    try expect(@TypeOf(addr) == usize);
    try expect(addr == 0xdeadbee0);
}

test "comptime @intToPtr" {
    comptime {
        // Zig is able to do this at compile-time, as long as
        // ptr is never dereferenced.
        const ptr = @intToPtr(*i32, 0xdeadbee0);
        const addr = @ptrToInt(ptr);
        try expect(@TypeOf(addr) == usize);
        try expect(addr == 0xdeadbee0);
    }
}
```

## @import
Import is how Zig includes files into a script. It is possible to use with both the standard library and with local files. Local files will need to use a path relative to the file including it, so you may have to traverse backwards to include files from an external folder.

A note is that only things declared with `pub` will be exposed to the source it is included into.

*An example of includes:*
```zig
// Standard library
const std = @import("std");

// Relative file
const my_file = @import("my_file.zig")
const another_file = @import("../another_file.zig")
```