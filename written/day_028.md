# 100 DAYS OF CODE : Day 28
**Note: Code samples are likely to be used from documentation**

## Allocators
When programming in many languages, it will handle memory allocations for you, or you may have some choice depending on the type of language. Zig has a whole new concept for this, which gives all the power to the programmer by allowing them to choose or define their own allocators. An allocator in Zig allows for programmers to choose how memory is assigned to the heap, since the language does not define a default allocator and because of this, it does not use any in its own standard library!

### Standard library and memory
Mentioned earlier, Zig does not manage memory and the standard library does not either! But how does it allocate memory for data structures if it doesn't manage memory? If a std library item requires allocations, it will have an argument that requires an allocator to be passed in.

The great thing about Zig allowing for allocators, including those defined by ourselves, we are able to use the standard library freestanding, this makes OS development quite easy in Zig.

### Zig Allocators and C
Since Zig allows for seamless interop with C, we will need to allocate memory at some point if we were to call C code. Zig handles this by offering a `c_allocator` to so that we do not break our C code and can continue our Zig operations.

### How do we choose an allocator?
Thankfully, Zig has quite an extensive list of questions to ask the programmer, which helps them choose based on the context of the data and how it is to be used.

### Choosing an Allocator? (std docs)
1. Are you making a library? In this case, best to accept an *Allocator as a parameter and allow your library's users to decide what allocator to use.
2. Are you linking libc? In this case, std.heap.c_allocator is likely the right choice, at least for your main allocator.
3. Is the maximum number of bytes that you will need bounded by a number known at comptime? In this case, use std.heap.FixedBufferAllocator or std.heap.ThreadSafeFixedBufferAllocator depending on whether you need thread-safety or not.
4. Is your program a command line application which runs from start to end without any fundamental cyclical pattern (such as a video game main loop, or a web server request handler), such that it would make sense to free everything at once at the end? In this case, it is recommended to follow this pattern:

```zig
const std = @import("std");

pub fn main() !void {
    var arena = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    defer arena.deinit();

    const allocator = &arena.allocator;

    const ptr = try allocator.create(i32);
    std.debug.print("ptr={*}\n", .{ptr});
}
```

When using this kind of allocator, there is no need to free anything manually. Everything gets freed at once with the call to arena.deinit().

5. Are the allocations part of a cyclical pattern such as a video game main loop, or a web server request handler? If the allocations can all be freed at once, at the end of the cycle, for example once the video game frame has been fully rendered, or the web server request has been served, then std.heap.ArenaAllocator is a great candidate. As demonstrated in the previous bullet point, this allows you to free entire arenas at once. Note also that if an upper bound of memory can be established, then std.heap.FixedBufferAllocator can be used as a further optimization.
6. Are you writing a test, and you want to make sure error.OutOfMemory is handled correctly? In this case, use std.testing.FailingAllocator.
7. Are you writing a test? In this case, use std.testing.allocator.
8. Finally, if none of the above apply, you need a general purpose allocator. Zig's general purpose allocator is available as a function that takes a comptime struct of configuration options and returns a type. Generally, you will set up one std.heap.GeneralPurposeAllocator in your main function, and then pass it or sub-allocators around to various parts of your application.
9. You can also consider Implementing an Allocator. 

### Types of Allocators
Based on your requirements, Zig offers several types of Allocators to create heaped memory. In the previous section, you can find out which type you may need. These allocators are:
- **page_allocator**: Possibly the simplest kind of allocator provided to us by Zig, it will call to our OS for pages of memory and these allocations of a single byte will usually reserve multiple kibibytes. Since asking the OS for memory requires a system call, this is also the most inefficient for speed.

```zig
const std = @import("std");
const expect = std.testing.expect;

test "allocation" {
    const allocator = std.heap.page_allocator;

    const memory = try allocator.alloc(u8, 100);
    defer allocator.free(memory);

    try expect(memory.len == 100);
    try expect(@TypeOf(memory) == []u8);
}
```

- **ArenaAllocator**: The Arena uses a child allocator to allocate memory multiple times, but is only required to free once using `deinit()` where `free` does not work.

```zig
test "arena allocator" {
    var arena = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    defer arena.deinit();
    var allocator = &arena.allocator;

    const m1 = try allocator.alloc(u8, 1);
    const m2 = try allocator.alloc(u8, 10);
    const m3 = try allocator.alloc(u8, 100);
}
```

- **FixedBufferAllocator**: An FixedBuffer does not allocate on the heap, it is used to create a fixed buffer when heap usage is not wanted or is limited. eg. Writing a kernel or for performance reasons.

```zig
test "fixed buffer allocator" {
    var buffer: [1000]u8 = undefined;
    var fba = std.heap.FixedBufferAllocator.init(&buffer);
    var allocator = &fba.allocator;

    const memory = try allocator.alloc(u8, 100);
    defer allocator.free(memory);

    try expect(memory.len == 100);
    try expect(@TypeOf(memory) == []u8);
}
```

- **GeneralPurposeAllocator**: The std library offers a general purpose allocator that prevents double-free, use-after-free and can detect leaks. Although you can configure it to turn off safety features, it is designed to be used for safety over performance and is yet several times faster than a `page_allocator`.

```zig
test "GPA" {
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer {
        const leaked = gpa.deinit();
        if (leaked) expect(false) catch @panic("TEST FAIL"); //fail test; can't try in defer as defer is executed after we return
    }
    const bytes = try gpa.allocator.alloc(u8, 100);
    defer gpa.allocator.free(bytes);
}
```

- **testing_allocator**: This is a special type of allocator that only works in tests and is able to detect leaks.

```zig
const eql = std.mem.eql;
const ArrayList = std.ArrayList;
const test_allocator = std.testing.allocator;

test "arraylist" {
    var list = ArrayList(u8).init(test_allocator);
    defer list.deinit();
    try list.append('H');
    try list.append('e');
    try list.append('l');
    try list.append('l');
    try list.append('o');
    try list.appendSlice(" World!");

    try expect(eql(u8, list.items, "Hello World!"));
}
```

- **c_allocator**: If our Zig code requires interop with C code, then we will have to consider a `c_allocator` to satisfy our C allocations.
- **StackFallbackAllocator**
- **ThreadSafeFixedBufferAllocator**

*Note: It is possible to allocate / deallocate for a single item, instead of multiple items, using `create()` and `destroy()`.*


This is just barely a fraction of what Zig Allocators are and their use-cases, but it is a start and hopefully covers enough to get the idea across. I am still learning and heavily relying on what is written in documentation and any learning resources the official Zig website links in their learning section.